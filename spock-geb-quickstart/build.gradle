/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2023 F. Hoffmann-La Roche Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.integration-test'
    id 'org.kordamp.gradle.functional-test'
    id 'signing'
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = 'com.roche.spock.geb'

repositories {
    mavenCentral()
}

if (!project.hasProperty('ossrhUsername')) ext.ossrhUsername = '**undefined**'
if (!project.hasProperty('ossrhPassword')) ext.ossrhPassword = '**undefined**'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Spock Geb Quickstart'
                description = 'This project simplifies executing Spock and Geb-based frontend tests.'
                url = 'https://github.com/Roche/spock-geb-quickstart'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jerzykrlkroche'
                        name = 'Jerzy Krolak'
                    }
                }
                scm {
                    connection = 'git@github.com:Roche/spock-geb-quickstart.git'
                    developerConnection = 'git@github.com:Roche/spock-geb-quickstart.git'
                    url = 'https://github.com/Roche/spock-geb-quickstart'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username "${ossrhUsername}"
                password "${ossrhPassword}"
            }
        }
    }

}

signing {
    sign publishing.publications.mavenJava
}

functionalTest {
    useJUnitPlatform()
}

ext {
    gebVersion = '7.0'
    seleniumVersion = '4.16.1'
    springBootVersion = '3.1.3'
    nettyVersion = '4.1.108.Final'
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.11'

    implementation('com.aoe:geb-spock-reports:0.3.0-RC1') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }

    implementation "com.athaydes:spock-reports:2.5.1-groovy-4.0"

    api "org.gebish:geb-spock:7.0"

    api group: 'org.spockframework', name: 'spock-core', version: '2.4-M1-groovy-4.0'
    api group: 'org.spockframework', name: 'spock-spring', version: '2.4-M1-groovy-4.0'

    api("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    implementation("io.github.bonigarcia:webdrivermanager:5.6.3")

    implementation "org.testcontainers:selenium:1.19.3"

    api "org.seleniumhq.selenium:selenium-remote-driver:${seleniumVersion}"

    api "org.seleniumhq.selenium:selenium-java:${seleniumVersion}"
    api "org.seleniumhq.selenium:selenium-api:${seleniumVersion}"
    api "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"

    implementation "io.netty:netty-all:${nettyVersion}"
}

test {
    useJUnitPlatform()
    systemProperties "geb.build.reportsDir": "$reportsDir/geb"
}
